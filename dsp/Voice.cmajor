namespace Synth
{
    graph Voice
    {
        input event (std::notes::NoteOn, std::notes::NoteOff) eventIn;
        input event Params paramsIn;

        input stream float osc1Gain;
        input stream float osc2Gain;

        input oscBank.wavetableIn1,
            oscBank.wavetableIn2,
            oscBank.vibrato1,
            oscBank.vibrato2,
            oscBank.vibratoDepth1,
            oscBank.vibratoDepth2;

        output stream float out;

        node
        {
            p = ParamSplitter;
            oscBank = Controllers::Oscillators;
            noteToFrequency = Util::NoteToFrequency;
            level1 = std::levels::SmoothedGain(float, 0.002f);
            level2 = std::levels::SmoothedGain(float, 0.002f);
            coarseTuner1 = Util::TuneCoarse;
            coarseTuner2 = Util::TuneCoarse;
            fineTuner1 = Util::TuneFine;
            fineTuner2 = Util::TuneFine;
            waveFolder1 = Common::WaveFolder;
            waveFolder2 = Common::WaveFolder;
            filter = Controllers::Filter;
            cutoffCalc = Common::FilterCutoffCalculator;
            brilliance = Common::Brilliance;
            brillianceMultiplier = Util::MultiplyStreams;
            gainCombiner = Util::MaxStreams;
        }

        connection
        {
            eventIn -> noteToFrequency;

            paramsIn -> oscBank.paramsIn;
            osc1Gain -> oscBank.osc1Gain;
            osc2Gain -> oscBank.osc2Gain;

            noteToFrequency -> coarseTuner1.in;
            noteToFrequency -> coarseTuner2.in;
            noteToFrequency -> cutoffCalc.noteFreq;

            // @todo: decide if brilliance should be the raw frequency or the "tuned" frequency
            noteToFrequency -> brilliance.frequencyIn;

            coarseTuner1.out -> fineTuner1.in;
            coarseTuner2.out -> fineTuner2.in;

            fineTuner1.out -> oscBank.osc1FreqIn;
            fineTuner2.out -> oscBank.osc2FreqIn;

            paramsIn -> p.paramsIn;

            p.osc1Level -> level1.volume;
            p.osc2Level -> level2.volume;
            p.osc1Coarse -> coarseTuner1.octave;
            p.osc2Coarse -> coarseTuner2.octave;
            p.osc1Fine -> fineTuner1.cents;
            p.osc2Fine -> fineTuner2.cents;

            p.osc1Wavefolding -> waveFolder1.gain;
            p.osc2Wavefolding -> waveFolder2.gain;

            p.filterBypass -> filter.bypass;
            p.cutoff -> cutoffCalc.baseCutoff;
            cutoffCalc.cutoff -> filter.cutoff;
            p.resonance -> filter.resonance;
            p.brilliance -> brilliance.amountIn;

            oscBank.out1 -> waveFolder1.in;
            oscBank.out2 -> waveFolder2.in;

            waveFolder1.out -> level1.in;
            waveFolder2.out -> level2.in;

            level1 -> filter.in;
            level2 -> filter.in;

            brilliance -> brillianceMultiplier.stream1;
            osc1Gain -> gainCombiner.stream1;
            osc2Gain -> gainCombiner.stream2;
            gainCombiner -> brillianceMultiplier.stream2;
            brillianceMultiplier -> out;

            filter.out -> out;
        }
    }
}