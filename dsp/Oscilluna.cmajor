namespace Synth
{
    graph Oscilluna  [[main]]
    {
        input event std::midi::Message midiIn;

        input waveControlPointParams.*;
        input paramsProcessor.*;

        output stream float<2> out;

        let voiceCount = VOICE_COUNT;

        node
        {
            paramsProcessor = ParamsProcessor;
            paramSplitter = ParamSplitter;
            voices = Controllers::Voice[voiceCount];
            builder1 = WaveBuilder;
            builder2 = WaveBuilder;
            voiceAllocator = std::voices::VoiceAllocator (voiceCount);
            waveControlPointParams = WaveControlPointParams;
            vibratoLfo1 = std::oscillators::LFO();
            vibratoLfo2 = std::oscillators::LFO();
            gainLimiter = std::levels::ConstantGain (float, 0.2f);
            chorus = Controllers::Chorus;
            phaser = Controllers::Phaser;
            reverb = Controllers::Reverb;
        }
    
        connection
        {
            midiIn -> std::midi::MPEConverter -> voiceAllocator;
            voiceAllocator.voiceEventOut -> voices.eventIn;
            paramsProcessor.paramsOut -> voices.paramsIn;
            paramsProcessor.paramsOut -> paramSplitter.paramsIn;
            paramsProcessor.paramsOut -> chorus.paramsIn;
            paramsProcessor.paramsOut -> phaser.paramsIn;
            paramsProcessor.paramsOut -> reverb.paramsIn;

            paramSplitter.osc1VibratoRate -> vibratoLfo1.rateHzIn;
            paramSplitter.osc2VibratoRate -> vibratoLfo2.rateHzIn;
            vibratoLfo1.out -> voices.vibrato1;
            vibratoLfo2.out -> voices.vibrato2;
            paramSplitter.osc1VibratoDepth -> voices.vibratoDepth1;
            paramSplitter.osc2VibratoDepth -> voices.vibratoDepth2;
            
            waveControlPointParams.controlPoints1 -> builder1.controlPoints;
            waveControlPointParams.controlPoints2 -> builder2.controlPoints;
            builder1.waveOut -> voices.wavetableIn1;
            builder2.waveOut -> voices.wavetableIn2;

            voices -> phaser.in;
            phaser -> gainLimiter;
            gainLimiter -> chorus.in;
            chorus -> Common::StereoLimiter -> reverb.in;
            reverb -> out;
        }
    }
}
