namespace Synth
{
    processor WavetableOscillator
    {
        input event float frequencyIn;
        input event float[] wavetableIn;
        input stream float gainIn;
        input stream float fmIn;
        input stream float fmExpIn;
        output stream float out;

        float64 phase = 0.0f;
        float64 phaseIncrement = 0.0;
        float freq = 0.0f;
        float[] wavetable;
        int wavetableIndex = 0;
        float prevfmIn = 0.0f;
        float prevSample = 0.0f;
        float prevPrevSample = 0.0f;
        float feedbackFmDepth = 0.0f;
        float invSampleRate = 1.0f / float(processor.frequency);
        int indexOffset;

        event frequencyIn (float f)
        {
            freq = f;
            wavetableIndex = Util::frequency_to_wavetable_index(freq, WAVETABLE_BUCKETS_MIN_FREQUENCY, WAVETABLE_BUCKETS_MAX_FREQUENCY, WAVETABLE_NUM_BUCKETS);
            indexOffset = wavetableIndex * TABLE_SIZE;
        }

        event wavetableIn (float[] newTable)    { wavetable = newTable; }

        void main()
        {
            float tableSizeFloat = float(TABLE_SIZE);
            float maxFmIndex = tableSizeFloat * 0.05f;
            float maxExpFmIndex = 0.01f;
 
            let referenceSampleRate = 44100.0f;
            let fmAdjust = (referenceSampleRate / float(processor.frequency));
            
            loop
            {
                let freqMod = freq * fmExpIn * maxExpFmIndex;
                phaseIncrement = (freq + freqMod) * (float(TABLE_SIZE) * invSampleRate);
                phase += phaseIncrement;
                phase += (fmIn + prevfmIn) * maxFmIndex * fmAdjust;
                prevfmIn = fmIn;
                
                phase -= floor(phase / tableSizeFloat) * tableSizeFloat;
                
                let index = wrap<TABLE_SIZE>(int32(phase));
                let nextIndex = wrap<TABLE_SIZE>(index + 1);
                let prevIndex = wrap<TABLE_SIZE>(index - 1);
                let nextNextIndex = wrap<TABLE_SIZE>(index + 2);
                
                float frac = float(phase) - float(index);
                
                // Fetch the four points needed for the interpolation
                float value0 = wavetable[indexOffset + prevIndex];
                float value1 = wavetable[indexOffset + index];
                float value2 = wavetable[indexOffset + nextIndex];
                float value3 = wavetable[indexOffset + nextNextIndex];
                 
                // Perform Hermite interpolation
                float slope0 = (value2 - value0) * 0.5f;
                float slope1 = (value3 - value1) * 0.5f;
                float c0 = value1;
                float c1 = slope0;
                float c2 = 3.0f * (value2 - value1) - 2.0f * slope0 - slope1;
                float c3 = 2.0f * (value1 - value2) + slope0 + slope1;
                float outputSample = ((c3 * frac + c2) * frac + c1) * frac + c0;
                
                prevPrevSample = prevSample;
                prevSample = outputSample;

                out <- outputSample * gainIn;
                
                advance();
            }
        }
    }
}